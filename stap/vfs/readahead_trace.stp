#!/usr/bin/env stap

/*
 this script will help trace readahead of specific file system (e.g. "ceph")
*/

global target_fs = "ceph";

function target_file_system(inode)
{
  fs = @cast(inode, "inode")->i_sb->s_type->name;
  if (kernel_string(fs) == target_fs)
	return 1;
  return 0;
}

probe kernel.function("ondemand_readahead").call
{
  inode = @cast($mapping, "address_space")->host;
  if (target_file_system(inode)) {
	printf("ONDEMAND: ino: %lu, off: %lu, req_size: %lu, marker: %d\n",
		   @cast(inode, "inode")->i_ino, $offset, $req_size,
		   $hit_readahead_marker);
  }
}

probe kernel.function("__do_page_cache_readahead").call
{
  inode = @cast($mapping, "address_space")->host;
  if (target_file_system(inode)) {
	printf("DOREAD: ino: %lu, off: %lu, to_read: %lu, lookahead: %lu\n",
		   @cast(inode, "inode")->i_ino, $offset, $nr_to_read,
		   $lookahead_size);
	ra = &@cast($filp, "file")->f_ra;
	printf("        ra_off: %lu, ra_size: %lu, ra_asize: %lu\n",
		   @cast(ra, "file_ra_state")->start,
		   @cast(ra, "file_ra_state")->size,
		   @cast(ra, "file_ra_state")->async_size);
	printf("\n");
  }
}
